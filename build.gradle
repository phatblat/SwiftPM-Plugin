/*
 * build.gradle
 * SwiftPM-Plugin
 */

/* -------------------------------------------------------------------------- */
// Build Script
/* -------------------------------------------------------------------------- */

buildscript {
    ext {
        kotlinVersion = '1.1.3'
        junitPlatformVersion = '1.0.0-M4'
    }
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven'
apply plugin: 'org.junit.platform.gradle.plugin'

task removeBatchFile(type: Delete) {
    delete 'gradlew.bat'
}

wrapper {
    gradleVersion = '4.0'
    distributionType = 'ALL'
//    distributionUrl = 'https://repo.gradle.org/gradle/dist-snapshots/gradle-kotlin-dsl-4.1-20170622074548+0000-all.zip'
    finalizedBy removeBatchFile
}

/* -------------------------------------------------------------------------- */
// Properties
/* -------------------------------------------------------------------------- */

group = 'at.phatbl'
version = '0.1.0'

ext {
    artifactName = name.toLowerCase()
    javaPackage = group + '.' + artifactName
    pluginClass =  name + 'Plugin'
}

archivesBaseName = javaPackage

/* -------------------------------------------------------------------------- */
// Build Configuration
/* -------------------------------------------------------------------------- */

repositories {
    maven { url 'http://dl.bintray.com/jetbrains/spek' }
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile gradleApi()

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
/*    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"*/
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    // Spock
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4', {
        exclude module: 'groovy-all'
    }

    // Speck
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testCompile 'org.jetbrains.spek:spek-api:1.1.2'

    testCompile 'org.jetbrains.spek:spek-junit-platform-engine:1.1.2'
    testCompile "org.junit.platform:junit-platform-engine:$junitPlatformVersion"
    testCompile "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
    testCompile "org.junit.platform:junit-platform-runner:$junitPlatformVersion"

    // JUnit 4
    //testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M4'

    // For using JUnit 4
    //testCompile 'junit:junit:4.12'
}

// https://docs.gradle.org/4.0/release-notes.html#multiple-class-directories-for-a-single-source-set

sourceSets {
    main {
        // FIXME: Output still going to classes/java/main
        kotlin.outputDir = file "$buildDir/classes/kotlin/main"
    }
    test {
        // FIXME: Output still going to classes/java/test
        kotlin.outputDir = file "$buildDir/classes/kotlin/test"
    }
}

// Make kotlin casses visible while Groovy in compiling
compileGroovy.classpath += files compileKotlin.destinationDir
compileTestGroovy.classpath += files compileTestKotlin.destinationDir

task showSourceSets {
    doLast {
        println "sourceSets.main.allSource.srcDirs"
        sourceSets.main.allSource.srcDirs.each { println it }

        ['kotlin', 'groovy', 'java'].each {
            println "sourceSets.main.${it}.outputDir: ${sourceSets.main["${it}"].outputDir}"
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

/* -------------------------------------------------------------------------- */
// Testing
/* -------------------------------------------------------------------------- */

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// org.junit.platform.gradle.plugin
junitPlatform {
    platformVersion junitPlatformVersion
    filters {
        engines {
            include 'spek'
        }
    }
}

/* -------------------------------------------------------------------------- */
// Deployment
/* -------------------------------------------------------------------------- */

// java-gradle-plugin
gradlePlugin {
    plugins {
        swiftpm {
            id = artifactName
            implementationClass = "$javaPackage.$pluginClass"
        }
    }
}

// maven
uploadArchives {
    repositories.mavenDeployer {
        repository url: "file://$buildDir/maven/repo"
        pom.artifactId = artifactName
    }
}
