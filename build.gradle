/*
 * build.gradle
 * SwiftPM-Plugin
 */

/* -------------------------------------------------------------------------- */
// Build Script
/* -------------------------------------------------------------------------- */

buildscript {
    ext.kotlin_version = '1.1.3'
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "org.jetbrains.kotlin.plugin.noarg"
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven'

task removeBatchFile(type: Delete) {
    delete 'gradlew.bat'
}

wrapper {
    gradleVersion = '4.1'
    distributionType = 'ALL'
    distributionUrl = 'https://repo.gradle.org/gradle/dist-snapshots/gradle-kotlin-dsl-4.1-20170622074548+0000-all.zip'
    finalizedBy removeBatchFile
}

/* -------------------------------------------------------------------------- */
// Build Configuration
/* -------------------------------------------------------------------------- */

repositories {
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4', {
        exclude module: 'groovy-all'
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

/* -------------------------------------------------------------------------- */
// Properties
/* -------------------------------------------------------------------------- */

group = 'at.phatbl'
version = '0.1.0'

ext {
    artifactName = name.toLowerCase()
    javaPackage = group + '.' + artifactName
    pluginClass =  name + 'Plugin'
}

archivesBaseName = javaPackage

/* -------------------------------------------------------------------------- */
// Deployment
/* -------------------------------------------------------------------------- */

// java-gradle-plugin
gradlePlugin {
    plugins {
        swiftpm {
            id = artifactName
            implementationClass = "$javaPackage.$pluginClass"
        }
    }
}

// maven
uploadArchives {
    repositories.mavenDeployer {
        repository url: "file://$buildDir/maven/repo"
        pom.artifactId = artifactName
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
