/*
 * build.gradle
 * SwiftPM-Plugin
 */

/* -------------------------------------------------------------------------- */
// Build Script
/* -------------------------------------------------------------------------- */

buildscript {
    ext.kotlinVersion = '1.1.3'
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

apply plugin: "org.jetbrains.kotlin.plugin.noarg"
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven'
apply plugin: 'org.junit.platform.gradle.plugin'

task removeBatchFile(type: Delete) {
    delete 'gradlew.bat'
}

wrapper {
    gradleVersion = '4.1'
    distributionType = 'ALL'
    distributionUrl = 'https://repo.gradle.org/gradle/dist-snapshots/gradle-kotlin-dsl-4.1-20170622074548+0000-all.zip'
    finalizedBy removeBatchFile
}

/* -------------------------------------------------------------------------- */
// Build Configuration
/* -------------------------------------------------------------------------- */

repositories {
    maven { url "http://dl.bintray.com/jetbrains/spek" }
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    //compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"

    // Spock
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4', {
        exclude module: 'groovy-all'
    }

    // Speck
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    testCompile 'org.jetbrains.spek:spek-api:1.1.2', {
        exclude group: 'org.jetbrains.kotlin'
    }

    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.2', {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    //testCompile 'org.jetbrains.kotlin:kotlin-test-junit:1.0.0-beta-4584'

    // For using JUnit 4
    //compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    //testCompile 'junit:junit:4.12'
    //testCompileClasspath 'org.jetbrains.spek:spek-junit-platform-engine:1.1.2'
    //testCompileClasspath 'org.junit.platform:junit-platform-runner:1.0.0-M4'
}

// https://docs.gradle.org/4.0/release-notes.html#multiple-class-directories-for-a-single-source-set
//sourceSets.main.output.classesDir = file("$buildDir/classes/main")
//sourceSets.test.output.classesDir = file("$buildDir/classes/test")

/* -------------------------------------------------------------------------- */
// Properties
/* -------------------------------------------------------------------------- */

group = 'at.phatbl'
version = '0.1.0'

ext {
    artifactName = name.toLowerCase()
    javaPackage = group + '.' + artifactName
    pluginClass =  name + 'Plugin'
}

archivesBaseName = javaPackage

/* -------------------------------------------------------------------------- */
// Testing
/* -------------------------------------------------------------------------- */

// org.junit.platform.gradle.plugin
junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

/* -------------------------------------------------------------------------- */
// Deployment
/* -------------------------------------------------------------------------- */

// java-gradle-plugin
gradlePlugin {
    plugins {
        swiftpm {
            id = artifactName
            implementationClass = "$javaPackage.$pluginClass"
        }
    }
}

// maven
uploadArchives {
    repositories.mavenDeployer {
        repository url: "file://$buildDir/maven/repo"
        pom.artifactId = artifactName
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
